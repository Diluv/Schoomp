plugins {
    id 'java'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
}

version = "${project_version}" + (isSnapshot() ? '+' + getBuildNumber() : '')
description = "Schoomp is a Java library for building and sending webhook requests to Discord"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation group: 'com.github.spotbugs', name: 'spotbugs', version: '4.0.0-beta5'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Creates a JAR containing the source code.'
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Creates a JAR containing the JavaDocs.'
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    manifest {
        attributes([
                'Timestamp'               : System.currentTimeMillis(),
                'Specification-Title'     : project.archivesBaseName,
                'Specification-Vendor'    : project.vendor,
                'Specification-Version'   : project.version,
                'Implementation-Title'    : project.archivesBaseName,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : project.vendor,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    ext."signing.gnupg.keyName" = "A35B3CEC"
}

signing {
    useGpgCmd()
    sign publishing.publications
}

tasks.withType(Sign) {
    onlyIf { !isSnapshot() }
}

nexusStaging {
    packageGroup = 'com.diluv'
    numberOfRetries = 40
    delayBetweenRetriesInMillis = 5000
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/Diluv/Schoomp'

                scm {
                    connection = 'scm:git:git://github.com/Diluv/Schoomp.git'
                    developerConnection = 'scm:git:ssh://github.com/Diluv/Schoomp.git'
                    url = 'https://github.com/Diluv/Schoomp'
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'lclc98'
                        name = 'lclc98'
                        email = 'lclc98@lclc98.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = System.getenv("OSSRH_USERNAME")
    password = System.getenv("OSSRH_PASSWORD")
}

static String getBuildNumber() {
    return System.getenv("GITHUB_RUN_NUMBER") ?: "0"
}

static boolean isSnapshot() {
    String ref = System.getenv("GITHUB_REF");
    if (ref != null && ref.startsWith("refs/tags/v")) {
        return false
    }
    return true
}